plugins {
    id 'org.springframework.boot' version "$springBoot"
    id 'application'
    id 'org.graalvm.buildtools.native' version "$graalvmBuildVersion"
}

java {
    sourceCompatibility = 21
    targetCompatibility = 21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':lib:ac-api')
    implementation "net.sf.applecommander:ShrinkItArchive:$shkVersion"
    implementation "net.sf.applecommander:applesingle-api:$asVersion"
    implementation "net.sf.applecommander:bastools-api:$btVersion"

    compileOnly "org.apache.ant:ant:$antVersion"

    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'com.webcodepro.applecommander.ui.ac'
}

jar {
    manifest {
        attributes 'Implementation-Title': "AppleCommander 'ac'",
                'Implementation-Version': archiveVersion
    }
}

bootJar {
    archiveBaseName = 'AppleCommander'
    archiveAppendix = 'ac'
    manifest {
        attributes 'Implementation-Title': "AppleCommander 'ac'",
                   'Implementation-Version': archiveVersion
    }
    from('../../LICENSE')
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("ac-${osName}-${osArch}-${project.version}")
        }
    }
}
