plugins {
    id 'org.springframework.boot' version "$springBoot"
    id 'application'
}

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    flatDir {
        // Raspberry Pi 32-bit library
        dirs project(':').projectDir.absolutePath + "/swt-lib"
    }
}

dependencies {
    implementation project(':lib:ac-swt-common')

    // This is a special case until 32-bit SWT libraries become available.
    implementation("org.eclipse.platform:org.eclipse.swt.gtk.linux.arm:3.8.2") {
        exclude group: "org.eclipse.platform", module: "org.eclipse.swt"
    }
}

application {
    mainClass = 'com.webcodepro.applecommander.ui.swt.SwtAppleCommander'
}

bootJar {
    archiveBaseName = 'AppleCommander'
    archiveAppendix = 'linux-arm'
    manifest {
        attributes 'Implementation-Title': 'AppleCommander (Linux Arm)',
                   'Implementation-Version': archiveVersion
    }
    from('../../LICENSE')
}
